---
layout: mypost
title: Verilog 备忘
categories: [备忘，在写了]
---

todo: 把 verilator 加上来

# Verilog 备忘

- [Verilog 备忘](#verilog-备忘)
  - [语法速查](#语法速查)
    - [示例代码](#示例代码)
    - [operator](#operator)
  - [防踩坑指南](#防踩坑指南)
    - [module](#module)
    - [procedures（包括 always, if, case)](#procedures包括-always-if-case)
      - [Combinational: `always @(*)`](#combinational-always-)
      - [Clocked: `always @(posedge clk)`](#clocked-always-posedge-clk)
      - [case， casez 语句](#case-casez-语句)
      - [避免生成锁存器](#避免生成锁存器)
    - [wire, reg](#wire-reg)
      - [implicit net](#implicit-net)
    - [vector](#vector)
      - [Unpacked arrays vs. Packed arrays](#unpacked-arrays-vs-packed-arrays)
      - [vector 连接符与 replication operator](#vector-连接符与-replication-operator)
    - [其他特性](#其他特性)
      - [三目运算符](#三目运算符)
      - [reduction operators](#reduction-operators)
  - [常见代码手法](#常见代码手法)
    - [根据真值表写电路](#根据真值表写电路)
    - [判断两 vector 相等](#判断两-vector-相等)
    - [错位比较](#错位比较)
    - [a 4-bit wide, 256-to-1 multiplexer](#a-4-bit-wide-256-to-1-multiplexer)
    - [Synchronous & Asynchronous Reset](#synchronous--asynchronous-reset)

## 语法速查
### 示例代码
```verilog
module 模块名 (
    input 端口名，
    output 端口名
);

    wire w1, w2;
    assign w1 = w2;
    // 条件表达式不一定像 Java 那样写成一个 xxx == 1'b1，可以用 C 风格的判断
    assign w = xxx ? w1 : w2;

    wire [100:0] v1;
    reg [0:10] v2;
    assign v1 = {10{w1}, 1'b1, 2'd2, 10'f8, w2};
    assign all_and = & v1;

    //------------------"+:"--------------
    data[begin +: width]
    data[(begin + width -1): begin]//这里是上式的等价表达式
    //------------------"-:"--------------
    data[end   -: width]
    data[end :(end-width+1)]       //这里是上式的等价表达式

    reg r1, r2;
    always @(*) begin
        r1 = 1'b1;
        // 0, x, z 为假，否则为真
        if (xxx) begin
            r1 = xxx;
        end

        if (xxx) begin
            xxx
        end
        else if (xxx) begin
            xxx
        end
        else begin
            xxx
        end

        case (in)
            1'b1: begin
                      xxx
                  end
            1'b0: xxx
            default: xxx
        endcase

        casez (in[3:0])
            4'bzzz1: out = 0;
            4'bzz1z: out = 1;
            4'bz1zz: out = 2;
            4'b1zzz: out = 3;
            default: out = 0;
        endcase

        // $bits() is a system function that returns the width of a signal.
        for (int i=0;i<$bits(out);i++)
            // $bits(out) is 100 because out is 100 bits wide.	
			out[i] = in[$bits(out)-i-1];
    end

    mod m(wa, wb, wc);
    mod m(.out(wc), .a(wa), .b(wb));

endmodule
```
### operator
bitwise 和 logical 运算符：
- bitwise 是按位运算，n 输入就是 n 输出
- logical 是逻辑运算，n 输入 1 输出
- bitwise 用来进行位运算，logical 用来进行逻辑判断

| operator type | not | and | or   | xor |
| ------------- | --- | --- | ---- | --- |
| bitwise       | ~   | &   | \|   | ^   |
| logical       | !   | &&  | \|\| |     |

## 防踩坑指南
### module
一对 module-endmodule 中不能再嵌套 module-endmodule。module 的使用方法叫实例化，而不是调用

两种端口连接方法：
```verilog
// 已知有如下 module 声明
module mod ( input a, input b, output out );

// 方法一
mod m(wa, wb, wc); // 其中 wa, wb, wc 对应 mod 中的 a, b, out，顺序必须和形参顺序一样

// 方法二
mod m(.out(wc), .a(wa), .b(wb)); // wa, wb, wc 的顺序无所谓
```
其中，如果端口是 vector，则连接时实参如果宽度不匹配，会进行零扩展或截断

### procedures（包括 always, if, case)
`assign`的左边必须是`net`类型（比如`wire`），`always`语句块中左边必须是`variable`类型（比如`reg`）

不要在 procedure 块里面写`assign`，不要在 procedure 块外面写 if-else

#### Combinational: `always @(*)`
本语句用来生成组合逻辑。使用 combinational always block 与使用 assign 等价，但是不要在 always 里写 assign。不要在 () 中写变量名。只需把`always @(*)`当成模板用就可以了

在 combinational always 中必须只使用 blocking assignment `=`
```verilog
reg a;
always @(*) begin
    a = 1'b1; // 正确
    a <= 1'b2; // 不要这么写
end
```
#### Clocked: `always @(posedge clk)`
此语句块用来生成时序逻辑。在 clocked always 中必须只使用 non-blocking assignment `<=`
```verilog
reg a;
always @(posedge clk) begin
    a <= 1'b2;
end
```

In Verilog-1995 the signals are separated by the keyword or. In Verilog-2001 the signals may be separated by a comma. This new comma-separated sensitivity list does not add new functionality.

#### case， casez 语句
不用写 break，case item 可以重复，以第一个匹配的项为准

casez 的匹配按从上往下顺序，以第一个匹配到的准，`z`也可以写成`x, ?`

#### 避免生成锁存器
只要避免有的信号没有被赋值就可以，总的来说就是不能让电路“记住”上一个时刻的的值（即 always 语句执行了一遍后有的变量根本没有更新动作，更新之后值和原来一样的情况不算）。常用做法有：
1. 必须在所有分支上给输出信号赋值，比如记得在 else 或者 default 中赋值
2. 给定初始值

```verilog
always @(*) begin
    // 所有分支都给信号一个值
    if (cpu_overheated)
        shut_off_computer = 1;
    else
        shut_off_computer = 0;

    // 给定初值
    left = 1'b0; down = 1'b0; right = 1'b0; up = 1'b0;
    case (scancode)
        16'he06b: left = 1'b1;
        16'he072: down = 1'b1;
        16'he074: right = 1'b1;
        16'he075: up = 1'b1;
    endcase
end
```

### wire, reg
`wire`在 verilog 中是有方向的，赋值时使用`assign`，所有的`assign`都是并行完成的。assign 时如果左右长度不匹配，则右方会进行零扩展或被截断

不能给`wire`进行初始化，但是可以给`reg`初始化

#### implicit net
wire 是一种 net 类型。对于 net 类型，当出现以下两种情况：一、在 assign 时没有被声明；二、是把一个没声明的 net-type 连到 module 端口上。此时 net 类型被隐式创建，且默认为 1-bit 宽

```verilog
wire a;
assign b = a; // 凭空出现了 wire b，默认为 1-bit
my_module (d, e); // 凭空出现了 d e，默认为 1-bit
```
在源文件开头加上 \`default_nettype 可以阻止 implicit net 生成

### vector
vector 的维度在名字之前，可以用下标访问
```verilog
wire [99:0] vec, out; // 两个 vector
assign out[2:0] = vec[3:1];
```

可选格式
```verilog
output/input wire/reg [upper:lower] vec_name;
```
- 中括号中的 range 可以为负，`reg [1:-1] vec` 是三个 `reg` 构成的 `vector` ，下标访问时也可以为负，比如`z[-1:-2]`表示`z`的最低二字节
- 小端存储：`wire [3:0] v`；大端存储：`wire [0:3] v`

下标访问的上下界顺序必须和声明时一致，即不能声明为小端`vector`却用大端方式访问，以下代码为错误代码

```verilog
wire [2:0] w1;
assign xxx = w1[0:2]; // 错误：声明时为上界在前，下界在后，则访问时必须为上界在前，下界在后
```

#### Unpacked arrays vs. Packed arrays
```verilog
reg [7:0] mem [255:0]; // 256 个 unpacked 元素，每个元素都是一个 8-bit reg（packed vector）
reg mem [28:0];  // 29 个 unpacked 元素，每个元素都是 1-bit 的 reg
```

#### vector 连接符与 replication operator
连接符可以出现在 assign 的左边或右边，如果左右宽度不一致，则超出去的宽度不会被赋值

大括号内出现的常量必须指定宽度，`{1'b1, 4'hf, 2'd2}`正确，`{1, 2, 3}` 错误
```verilog
{3'b111, 4'ha} // 表示 7'b1111010
```

replication 的外面必须有一层大括号
```verilog
{5{1'b1}} // 表示 5'b11111
{2{a, b}} // 表示 {a b a b}
```

### 其他特性
#### 三目运算符
格式和 C 一样，用来生成多路选择器。写在 procedure 里面或外面都可以
```verilog
((sel[1:0] == 2'h0) ? a :     // A 3-to-1 mux
 (sel[1:0] == 2'h1) ? b :
                      c )
```

#### reduction operators
常用于把一个 vector 的所有位都异或起来

```verilog
& a[3:0]     // AND: a[3]&a[2]&a[1]&a[0]. Equivalent to (a[3:0] == 4'hf)
| b[3:0]     // OR:  b[3]|b[2]|b[1]|b[0]. Equivalent to (b[3:0] != 4'h0)
^ c[2:0]     // XOR: c[2]^c[1]^c[0]
```

## 常见代码手法
### 根据真值表写电路
利用最小项之积即可，例：
```verilog
module top_module( 
    input x3,
    input x2,
    input x1,  // three inputs
    output f   // one output
);
    assign f = ~x3 & x2 & ~x1 | ~x3 & x2 & x1 | x3 & ~x2 & x1 | x3 & x2 & x1;
endmodule
```

### 判断两 vector 相等
```verilog
module top_module ( input [1:0] A, input [1:0] B, output z ); 
    // 直接写 A == B 也行，不用专门写一个三目运算符
	assign z = (A[1:0] == B[1:0]);
endmodule
```

### 错位比较
原题：https://hdlbits.01xz.net/wiki/Gatesv

解析：`out_both`和`out_any`可以各从`in`中拿出正好差了一个下标位置的 3 位来，进行位运算即可得结果。`out_different`是手动做了一个`in`循环右移一位的临时 vector，然后和原 vector 按位异或
```verilog
module top_module (
	input [3:0] in,
	output [2:0] out_both,
	output [3:1] out_any,
	output [3:0] out_different
);

	// Use bitwise operators and part-select to do the entire calculation in one line of code
	// in[3:1] is this vector:   					 in[3]  in[2]  in[1]
	// in[2:0] is this vector:   					 in[2]  in[1]  in[0]
	// Bitwise-OR produces a 3 bit vector.			   |      |      |
	// Assign this 3-bit result to out_any[3:1]:	o_a[3] o_a[2] o_a[1]

	// Thus, each output bit is the OR of the input bit and its neighbour to the right:
	// e.g., out_any[1] = in[1] | in[0];	
	// Notice how this works even for long vectors.
	assign out_any = in[3:1] | in[2:0];

	assign out_both = in[2:0] & in[3:1];
	
	// XOR 'in' with a vector that is 'in' rotated to the right by 1 position: {in[0], in[3:1]}
	// The rotation is accomplished by using part selects[] and the concatenation operator{}.
	assign out_different = in ^ {in[0], in[3:1]};
	
endmodule
```

### a 4-bit wide, 256-to-1 multiplexer
The 256 4-bit inputs are all packed into a single 1024-bit input vector. sel=0 should select bits in[3:0], sel=1 selects bits in[7:4], sel=2 selects bits in[11:8], etc
```verilog
module top_module( 
    input [1023:0] in,
    input [7:0] sel,
    output [3:0] out );
    assign out = in[sel*4 +: 4];
endmodule
```

### Synchronous & Asynchronous Reset
见：https://vlsi.pro/synchronous-asynchronous-reset/

同步复位：
```verilog
module top_module (
    input clk,
    input reset,            // Synchronous reset
    input [7:0] d,
    output [7:0] q
);
    always @(posedge clk) begin
        if (reset) q <= 8'b0;
        else q <= d;
    end
endmodule
```

异步复位：
```verilog
module top_module (
    input clk,
    input areset,   // active high asynchronous reset
    input [7:0] d,
    output [7:0] q
);
    always @(posedge clk, posedge areset) begin
        if (areset) q <= 8'b0;
        else q <= d;
    end

endmodule
```